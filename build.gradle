plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'idea'

group 'org.jobrunr'
version '0.9-SNAPSHOT'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compile 'org.jobrunr:jobrunr:0.9.4'
    compile 'com.datastax.oss:java-driver-core:4.4.0'
    compile 'com.datastax.oss:java-driver-query-builder:4.4.0'

    testCompile 'org.junit.jupiter:junit-jupiter:5.6.1'
    testCompile 'org.jobrunr:jobrunr:0.9.4:test-fixtures'
    testCompile 'org.awaitility:awaitility:4.0.2'
    testCompile 'org.assertj:assertj-core:3.16.1'
}


test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr.storage'
            artifactId = 'cassandra'
            version = project.version.replace("v", "")
            from components.java

            pom {
                name = 'JobRunr Cassandra Storage Provider'
                description = 'A JobRunr Storage Provider for Cassandra.'
                url = 'https://github.com/rdehuyss/jobrunr-storage-cassandra'
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/rdehuyss/jobrunr-storage-cassandra.git'
                    developerConnection = 'scm:git@github.com:rdehuyss/jobrunr-storage-cassandra.git'
                    url = 'https://github.com/rdehuyss/jobrunr-storage-cassandra.git'
                }

                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        it.optional.text() == 'true'
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}